-- @path Source=/Simulink2SysML/MetaModels/SimulinkSourceMM.ecore
-- @path Simulink=/Simulink2SysML/MetaModels/SimulinkMM.ecore

module Source2SourceWConnections;
create srcwcon : Source from src : Source, simLines : Simulink;

rule Model2Model {
	from
		mi: Source!Model
	to
		mo: Source!Model (
			Name <- mi.Name,
			Block <- mi.Block
		)
}

rule System2System {
	from
		bi: Source!System
	to
		bo: Source!System (
			Name <- bi.Name,
			ID <- bi.ID,
			Property <- bi.Property,
			Block <- bi.Block,
			Port <- bi.Port,
			Connection <- Simulink!Line.allInstancesFrom('simLines')->
					select(sl | sl.getIDLine = bi.ID and sl.Branch->size() = 0)->
					collect(slp | thisModule.Line2Connection(slp))->
					union(Simulink!Branch.allInstancesFrom('simLines')->
					select(sb | sb.getIDBranch = bi.ID and sb.Branch->size() = 0)->
					collect(sbp | thisModule.Branch2Connection(sbp)))
		)
}

lazy rule Branch2Connection {
	from
		b: Simulink!Branch
	to
		c: Source!Connection (
			Name <- b.Property->select(sb | sb.Name = 'Name')->
				collect(sbv | sbv.Value.Value)->any(sbs | true),
			src <- thisModule.findPort(b.getSrc),
			dst <- thisModule.findPort(b.Property->select(db | db.Name.toLower() = 'dst')->
				collect(dbv | dbv.Value.Value)->any(dbs | true))
		)
}

lazy rule Line2Connection {
	from
		l: Simulink!Line
	to
		c: Source!Connection (
			Name <- l.Property->select(sl | sl.Name = 'Name')->
				collect(slv | slv.Value.Value)->any(sls | true),
			src <- thisModule.findPort(l.Property->select(sl | sl.Name.toLower() = 'src')->
				collect(slv | slv.Value.Value)->any(sls | true)),
			dst <- thisModule.findPort(l.Property->select(dl | dl.Name.toLower() = 'dst')->
				collect(dlv | dlv.Value.Value)->any(dls | true))
		)
}

helper context Simulink!Line def: getIDLine: String =
	if self.refImmediateComposite().refImmediateComposite().oclIsTypeOf(Simulink!Model) then
		'Root'
	else
		self.refImmediateComposite().refImmediateComposite().SID
	endif;

helper context Simulink!Branch def: getIDBranch: String =
	if self.refImmediateComposite().oclIsTypeOf(Simulink!Line) then
		self.refImmediateComposite().getIDLine
	else
		self.refImmediateComposite().getIDBranch
	endif;

helper context Simulink!Branch def: getSrc: String =
	if self.refImmediateComposite().oclIsTypeOf(Simulink!Line) then
		self.refImmediateComposite().Property->select(sl | sl.Name.toLower() = 'src')->
				collect(slv | slv.Value.Value)->any(sls | true)
	else
		self.refImmediateComposite().getSrc
	endif;

helper def: findPort(pName : String): Source!Port =
	Source!Port.allInstancesFrom('src')->
	select(p | p.refImmediateComposite().ID + '#' + p.Direction + ':' + p.Num = pName)->any(sp | true);

rule Block2Block {
	from
		bi: Source!Block
	to
		bo: Source!Block (
			Name <- bi.Name,
			ID <- bi.ID,
			Type <- bi.Type,
			Property <- bi.Property,
			Port <- bi.Port
		)
}

rule Property2Property {
	from
		pi: Source!Property
	to
		po: Source!Property (
			Name <- pi.Name,
			Type <- pi.Type,
			Value <- pi.Value
		)
}

rule Port2Port {
	from
		pi: Source!Port
	to
		po: Source!Port (
			Direction <- pi.Direction,
			Num <- pi.Num,
			Name <- pi.Name,
			Type <- pi.Type
		)
}