-- @path Source=/Simulink2SysML/MetaModels/SimulinkSourceMM.ecore

module Source2SourceWPorts;
create srcwports : Source from src : Source;

rule Model2Model {
	from
		mi: Source!Model
	to
		mo: Source!Model (
			Name <- mi.Name,
			Block <- mi.Block
		)
}

rule System2SystemWPorts {
	from
		bi: Source!System (bi.hasPortsProperty)
	to
		bo: Source!System (
			Name <- bi.Name,
			ID <- bi.ID,
			Property <- bi.Property,
			Port <- thisModule.generateSequence(bi.getInportCount)->collect(i | thisModule.createInPort(i.toString() + thisModule.nameDelim + bi.getPortName(i, 'in') + thisModule.typeDelim))->
				union(thisModule.generateSequence(bi.getOutportCount)->collect(o | thisModule.createOutPort(o.toString() + thisModule.nameDelim + bi.getPortName(o, 'out') + thisModule.typeDelim))),
			Block <- bi.Block
		)
}

rule System2System {
	from
		bi: Source!System (not bi.hasPortsProperty)
	to
		bo: Source!System (
			Name <- bi.Name,
			ID <- bi.ID,
			Property <- bi.Property,
			Block <- bi.Block
		)
}

rule Block2BlockWInport {
	from
		bi: Source!Block(bi.needsInport and not bi.needsOutport)
	to
		bo: Source!Block (
			Name <- bi.Name,
			ID <- bi.ID,
			Type <- bi.Type,
			Property <- bi.Property,
			Port <- thisModule.createInPort('1' + thisModule.nameDelim + thisModule.typeDelim)
		)
}

rule Block2BlockWOutport {
	from
		bi: Source!Block (not bi.needsInport and bi.needsOutport)
	to
		bo: Source!Block (
			Name <- bi.Name,
			ID <- bi.ID,
			Type <- bi.Type,
			Property <- bi.Property,
			Port <- thisModule.createOutPort('1' + thisModule.nameDelim + thisModule.typeDelim + bi.getPortType)
		)
}

rule Block2BlockWInOutport {
	from
		bi: Source!Block (bi.needsInport and bi.needsOutport)
	to
		bo: Source!Block (
			Name <- bi.Name,
			ID <- bi.ID,
			Type <- bi.Type,
			Property <- bi.Property,
			Port <- Sequence{thisModule.createInPort('1' + thisModule.nameDelim + thisModule.typeDelim)}
				->append(thisModule.createOutPort('1' + thisModule.nameDelim + thisModule.typeDelim))
		)
}

rule Block2BlockWPorts {
	from
		bi: Source!Block (not bi.needsInport and not bi.needsOutport and bi.hasPortsProperty)
	to
		bo: Source!Block (
			Name <- bi.Name,
			ID <- bi.ID,
			Type <- bi.Type,
			Property <- bi.Property,
			Port <- thisModule.generateSequence(bi.getInportCount)->collect(i | thisModule.createInPort(i.toString() + thisModule.nameDelim + bi.getPortName(i, 'in') + thisModule.typeDelim))->
				union(thisModule.generateSequence(bi.getOutportCount)->collect(o | thisModule.createOutPort(o.toString() + thisModule.nameDelim + bi.getPortName(o, 'out') + thisModule.typeDelim)))
		)
}

rule Block2Block {
	from
		bi: Source!Block (not bi.needsInport and not bi.needsOutport and not bi.hasPortsProperty)
	to
		bo: Source!Block (
			Name <- bi.Name,
			ID <- bi.ID,
			Type <- bi.Type,
			Property <- bi.Property
		)
}

rule Property2Property {
	from
		pi: Source!Property (not pi.isPortsProperty)
	to
		po: Source!Property (
			Name <- pi.Name,
			Type <- pi.Type,
			Value <- pi.Value
		)
}

lazy rule createInPort {
	from
		i : String -- form of {num}#{Name} + thisModule.nameDelim + thisModule.typeDelim
	to
		p : Source!Port(
			Direction <- 'in',
			Num <- i.substring(1, i.indexOf(thisModule.nameDelim)).toInteger(),
			Name <- i.substring(i.indexOf(thisModule.nameDelim) + thisModule.nameDelim.size() + 1, i.indexOf(thisModule.typeDelim)),
			Type <- i.substring(i.indexOf(thisModule.typeDelim) + thisModule.typeDelim.size() + 1, i.size())
		)
}

lazy rule createOutPort {
	from
		o : String -- form of {num}#{Name}
	to
		p : Source!Port(
			Direction <- 'out',
			Num <- o.substring(1, o.indexOf(thisModule.nameDelim)).toInteger(),
			Name <- o.substring(o.indexOf(thisModule.nameDelim) + thisModule.nameDelim.size() + 1, o.indexOf(thisModule.typeDelim)),
			Type <- o.substring(o.indexOf(thisModule.typeDelim) + thisModule.typeDelim.size() + 1, o.size())
		)
}

helper def: nameDelim : String = '#n#';
helper def: typeDelim : String = '#t#';

helper def: InOutportBlockTypeList : String = '|';
helper def: InOutportKeywordList : String = '|Ports|PortCounts|'; -- we assume this results in [i, o] format, and that if both occur, they will be same value
helper def: InportBlockTypeList : String = '|Terminator|Outport|';
helper def: InportKeywordList : String = '|Inputs|'; -- we assume a single integer
helper def: OutportBlockTypeList : String = '|Inport|';
helper def: OutportKeywordList : String = '|Outputs|'; -- we assume a singler integer

helper context Source!Component def: hasPortsProperty: Boolean =
	self.Property->exists(p | p.isPortsProperty);

helper context Source!Property def: isPortsProperty: Boolean =
	thisModule.InOutportKeywordList.indexOf('|' + self.Name + '|') > -1
		or thisModule.InportKeywordList.indexOf('|' + self.Name + '|') > -1
		or thisModule.OutportKeywordList.indexOf('|' + self.Name + '|') > -1;

helper context Source!Component def: needsInport: Boolean =
	thisModule.InportBlockTypeList.indexOf('|' + self.Type + '|') > -1;

helper context Source!Component def: needsOutport: Boolean =
	thisModule.OutportBlockTypeList.indexOf('|' + self.Type + '|') > -1;

helper context Source!Component def: getInportCount: Integer =
	if self.Property->exists(p | thisModule.InOutportKeywordList.indexOf('|' + p.Name + '|') > -1) then
		self.Property->select(i | thisModule.InOutportKeywordList.indexOf('|' + i.Name + '|') > -1)->first().Value.getFirstFrom2Array
	else
		if self.Property->exists(p | thisModule.InportKeywordList.indexOf('|' + p.Name + '|') > -1) then
			self.Property->select(i | thisModule.InportKeywordList.indexOf('|' + i.Name + '|') > -1)->first().Value.toInteger()
		else
			0
		endif
	endif;

helper context Source!Component def: getOutportCount: Integer =
	if self.Property->exists(p | thisModule.InOutportKeywordList.indexOf('|' + p.Name + '|') > -1) then
		self.Property->select(o | thisModule.InOutportKeywordList.indexOf('|' + o.Name + '|') > -1)->first().Value.getSecondFrom2Array
	else
		if self.Property->exists(p | thisModule.OutportKeywordList.indexOf('|' + p.Name + '|') > -1) then
			self.Property->select(o | thisModule.OutportKeywordList.indexOf('|' + o.Name + '|') > -1)->first().Value.toInteger()
		else
			0
		endif
	endif;

helper context String def: getFirstFrom2Array: Integer =
	self.substring(self.indexOf('[') + 2, self.indexOf(',')).toInteger();

helper context String def: getSecondFrom2Array: Integer =
	self.substring(self.indexOf(',') + 3, self.indexOf(']')).toInteger();

helper context Source!Component def: getPortName(num : Integer, dir : String): String = 
	self.Port->iterate(port; name : String = '' |
		if port.Num = num and port.Direction = dir then
			port.Name
		else
			name
		endif);

helper context Source!Component def: getPortType: String = 
	self.Property->iterate(prop; type : String = '' |
		if prop.Name = 'PortDimensions' then
			if prop.Type = 'Array:Int' then
				'Array:Float (' + prop.Value.getFirstFrom2Array + ')'
			else
				if prop.Type = 'Int' and prop.Value <> '-1' then
					prop.Value
				else
					type
				endif
			endif
		else
			type
		endif);


helper def : generateSequence(num : Integer): Sequence(Integer) =
	if num = 0 then
		Sequence{}
	else if num = 1 then
			Sequence{1}
		else
			thisModule.generateSequence(num - 1).append(num)
		endif
	endif;